import material/material_ui;
import material/extra/filebrowser/material_filebrowser;

printF3DChildren(id : string, object : F3DObject) -> F3DObject {
	switch (object : F3DObject) {
		F3DScene(items): {
			println(id + " Group");

			F3DScene(fmapi(items, \i, it -> printF3DChildren(id + (if (strlen(id) > 0) " " else "") + i2s(i), it)));
		}
		F3DNativeScene(clip, items): {
			println(id + " " + get3DObjectType(clip));

			F3DNativeScene(clip, fmapi(items, \i, it -> printF3DChildren(id + (if (strlen(id) > 0) " " else "") + i2s(i), it)));
		}
		F3DNativeObject(clip): {
			println(id + " " + get3DObjectType(clip));

			object;
		}
		F3DMutable(innerObject): {
			F3DMutable(fselect(innerObject, FLift(\it -> printF3DChildren(id, it))));
		}
		default : {
			println(id + " " + object.structname);

			object;
		}
	}
}

makeF3DTreeNode(
	manager : MaterialManager,
	id : string,
	name : string,
	attachFn : ref (native, bool) -> void,
	clip : Maybe<native>,
	children : Transform<[MTreeNode]>
) -> MTreeNode {
	sel = make(false);

	MTreeNode(
		makeTropicTag(manager),
		\__ ->
			MCols2(
				MText(id + "  ", [MCaptionSolid()]),
				MIf(
					sel,
					MText(name, [MCaptionSolid(), getPrimaryColor(manager)]),
					MText(name, [MCaption1()])
				)
			)
			|> (\f ->
				eitherMap(
					clip,
					\c ->
						MConstruct(
							[
								\ -> add3DEventListener(c, "attached", \ -> nextDistinct(sel, true)),
								\ -> add3DEventListener(c, "detached", \ -> nextDistinct(sel, false)),
								makeSubscribe2(sel, \sl -> ^attachFn(c, sl))
							],
							f
						),
					f
				)
			),
		children,
		[MExpanded(make(true)), MHighlightOnSelect(const(false)), MSelected(sel)]
	)
}

makeF3DTree(manager : MaterialManager, id : string, object : F3DObject, attachFn : ref (native, bool) -> void) -> Transform<MTreeNode> {
	println(object.structname);

	switch (object : F3DObject) {
		F3DScene(items): {
			const(makeF3DTreeNode(manager, id, "Group", attachFn, None(),
				fsubmapi(items, \i, it -> makeF3DTree(manager, i2s(i), it, attachFn))));
		}
		F3DNativeScene(clip, items): {
			const(makeF3DTreeNode(manager, id, get3DObjectType(clip), attachFn, Some(clip),
				fsubmapi(items, \i, it -> makeF3DTree(manager, i2s(i), it, attachFn))));
		}
		F3DNativeObject(clip): {
			const(makeF3DTreeNode(manager, id, get3DObjectType(clip), attachFn, Some(clip), const([])));
		}
		F3DMutable(innerObject): {
			fsubselect(innerObject, FLift(\it -> makeF3DTree(manager, id, it, attachFn)));
		}
		default : {
			const(makeF3DTreeNode(manager, id, object.structname, attachFn, None(), const([])));
		}
	}
}

main() {
	manager = makeMaterialManager([makeMaterialTheme(true, MPurpleA(700), MLightGreenA(700))]);

	currentScene = make([F3DSceneLoader("images/3d/soccer/soc_field.json")]);
	currentTree : DynamicBehaviour<Transform<[MTreeNode]>> = make(const([]));
	stageAvailable = makeWH();

	exportFn = ref \ -> "";
	attachFn = ref nop2;

	scene =
		F3DChildren(
			F3DScene(currentScene),
			\c -> {
				next(currentTree, farray(makeF3DTree(manager, "", c, attachFn)));

				printF3DChildren("", c);
			}
		);
	camera =
		F3DPosition(
			F3DPerspectiveCamera(const(50.0), fdivide(fwidth(stageAvailable), fheight(stageAvailable)), const(0.01), const(6000.0)),
			make(0.0), make(0.0), make(1000.0)
		);

	MLines2ReverseZorder(
		MMenuPanel(
			[
				MMenu(
					MTextButton("File", nop, [], []),
					[
						MMenuSingleLine(
							"Open Scene",
							[
								MPrimaryAction(MIcon("folder_open", [])),
								MOnClick(\ ->
									openFileDialog(1, ["*.txt", "*.json"], \ff ->
										iter(ff, \f ->
											readFileClient(
												f,
												"text",
												\s -> {
													next(currentScene, [F3DScene(const([]))]);
													next(currentScene, [F3DJsonObject(s)]);
												},
												println
											)
										)
									)
								)
							]
						),
						MMenuSingleLine(
							"Save Scene",
							[
								MPrimaryAction(MIcon("save", [])),
								MOnClick(\ -> {saveFileClient("scene.json", ^exportFn(), "text")})
							]
						)
					],
					[
						MMenuIndent()
					]
				)
			],
			[]
		),
		MProportionCols([
			MInspect(
				[
					IAvailable(stageAvailable)
				],
				M3DStage(
					stageAvailable,
					F3DScene(const([
						F3DAxesHelper(2500.0),
						F3DGridHelper(5000.0, 50, 0xaaaaaa, 0xffffff),
						scene
					])),
					camera,
					[
						F3DExportScene(exportFn),
						F3DTransformControlsEnabled(const(true)),
						F3DAttachTransformControls(attachFn)
					]
				)
				|> (\f -> MFrame(0.0, 0.0, [MFill(MGrey(200))], f))
			)
			|> (\f -> Pair(const(0.7), f)),
			MTree(fsubselect(currentTree, idfn |> FLift), [MTreeHideSeparators()])
			|> (\f -> Pair(const(0.3), f))
		])
	)
	|> MCenterX
	|> (\f -> mrender(manager, true, f))
}

MLines2ReverseZorder(c1 : Material, c2 : Material) -> Material {
	MCopySize2(
		c1,
		\sz, c -> MGroup2(MLines2(sz, c2), c)
	)
}