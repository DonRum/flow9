import dynamic;
import ds/tree;
import sys/target;

export {
	// This will reconstruct an object to try to save memory
	// Only works for JS.
	compactObject(o : flow) -> flow;
}

compactObject(o : flow) -> flow {
	doCompactObject(ref makeTree(), ref makeTree(), o);
}

doCompactObject(cache : ref Tree<flow, flow>, arrayCache : ref Tree<flow, flow>, o : flow) -> flow {
	if (isArray(o)) {
		mt = lookupTree(^arrayCache, o);
		mt ?? {
			mt;
		} : {
			arrayCache := setTree(^arrayCache, o, o);
			doCompactArray(cache, arrayCache, o);
		}
	} else if (isSameStructType(o, o)) {
		mt = lookupTree(^cache, o);
		mt ?? {
			mt;
		} : {
			cache := setTree(^cache, o, o);
			doCompactStruct(cache, arrayCache, o);
		}
	} else {
		o;
	}
}

doCompactArray(cache : ref Tree<flow, flow>, arrayCache : ref Tree<flow, flow>, a : [flow]) -> [flow] {
	map(a, \e -> {
		doCompactObject(cache, arrayCache, e)
	})
}

doCompactStruct(cache : ref Tree<flow, flow>, arrayCache : ref Tree<flow, flow>, o : flow) -> flow {
	if (js) {
		// Here, find the objects of the object and recurse on them
		args : [flow] = extractStructArguments(o);
		cargs = doCompactArray(cache, arrayCache, args);
		makeStructValue(o.structname, cargs, o);
	} else o;
}
