import material/internal/selectmarray;
import material/internal/material_dragdrop;
import material/internal/material_icons;
import ui/animate/teasing;

export {
	MTree2T(manager : MaterialManager, parent : MFocusGroup, m : MTree<?>, m2t : (Material, MFocusGroup) -> Tropic) -> Tropic;

	MTree2s(tree : MTree<?>) -> string;
	MTreeOrder2s(nodes : [MTreeOrderNode<?>]) -> string;
}

MTree2s(tree : MTree<?>) -> string {
	children = getDynamicArrayValue(tree.children);

	if (length(children) > 0) {
		"MTree([" + "\n" +
		strGlue(map(children, \c -> MTreeNode2s(c, "\t")), "\n") + "\n"
		"])"
	} else {
		"MTree()"
	}
}

MTreeNode2s(tree : MTreeNode<?>, prefix : string) -> string {
	children = getDynamicArrayValue(tree.children);

	if (length(children) > 0) {
		prefix + "MTreeNode(" + toString(tree.content) + ", [" + "\n" +
		strGlue(map(children, \c -> MTreeNode2s(c, prefix + "\t")), "\n") + "\n" +
		prefix + "])"
	} else {
		prefix + "MTreeNode(" + toString(tree.content) + ")"
	}
}

MTreeOrder2s(nodes : [MTreeOrderNode<?>]) -> string {
	if (length(nodes) > 0) {
		"MTreeOrder([" + "\n" +
		strGlue(map(nodes, \c -> MTreeOrderNode2s(c, "\t")), "\n") + "\n"
		"])"
	} else {
		"MTreeOrder()"
	}
}

MTreeOrderNode2s(tree : MTreeOrderNode<?>, prefix : string) -> string {
	children = getDynamicArrayValue(tree.children);

	if (length(children) > 0) {
		prefix + "MTreeOrderNode(" + toString(tree.content) + ", [" + "\n" +
		strGlue(map(getDynamicArrayValue(tree.children), \c -> MTreeOrderNode2s(c, prefix + "\t")), "\n") + "\n" +
		prefix + "])"
	} else {
		prefix + "MTreeOrderNode(" + toString(tree.content) + ")"
	}
}

MTreeState(
	items : DynamicArray<[int]>,
	selected : DynamicArray<[int]>,
	expanded : Maybe<MTreeExpanded>,
	color : MColor,
	multiSelection : bool,
	showSeparators : bool,
	dragDropState : Maybe<MTreeDragDropState<?>>,
	lazyScrollMode : Maybe<MTreeLazyScrollModeState>,
	loadLazyButtonMode : Maybe<MTreeLazyLoad>
);

MTreeDragDropState(
	order : DynamicArray<MTreeOrderNode<?>>,
	onChange : Maybe<(newOrder : [MTreeOrderNode<?>]) -> void>,
	content2id : (content : ?) -> int,
	id2content : (id : int) -> Maybe<?>,
	current : DynamicBehaviour<Maybe<(MFocusGroup) -> Tropic>>
);

MTreeLazyScrollModeState(
	enabled : Transform<bool>,
	scrollWH : DynamicBehaviour<WidthHeight>,
	scrollPositionScale : DynamicBehaviour<PositionScale>
);

dynamicSelectionIndexes(children : DynamicArray<MTreeNode<?>>) -> DynamicArray<[int]> {
	mapiDynamicArray(children, \i, c -> dynamicSelectionIndexes2(const([i]), c.children))
	|> mergeDynamicArray
}

dynamicSelectionIndexes2(index : Transform<[int]>, children : DynamicArray<MTreeNode<?>>) -> DynamicArray<[int]> {
	concatDynamicArray(
		selectDynamicArray(index, \i -> [i]),
		mapiDynamicArray(children, \i, c -> dynamicSelectionIndexes2(farrayPush(index, const(i)), c.children))
		|> mergeDynamicArray
	)
}

unwrapMTreeNodes(order : DynamicArray<MTreeNode<?>>) -> DynamicArray<MTreeNode<?>> {
	mergeDynamicArray(mapDynamicArray(order, \o -> concatDynamicArray(makeDynamicArray([o]), unwrapMTreeNodes(o.children))))
}

lengthMTreeNodes(children : DynamicArray<MTreeNode<?>>) -> Transform<int> {
	lengthDynamicArray(unwrapMTreeNodes(children));
}

MTreeNodes2MTreeOrderNodes(nodes : DynamicArray<MTreeNode<?>>) -> DynamicArray<MTreeOrderNode<?>> {
	mapDynamicArray(nodes, MTreeNode2MTreeOrderNode)
}

MTreeNode2MTreeOrderNode(node : MTreeNode<?>) -> MTreeOrderNode<?> {
	MTreeOrderNode(node.content, mapDynamicArray(node.children, MTreeNode2MTreeOrderNode))
}

unwrapMTreeOrderNodes(order : DynamicArray<MTreeOrderNode<?>>) -> DynamicArray<MTreeOrderNode<?>> {
	mergeDynamicArray(mapDynamicArray(order, \o -> concatDynamicArray(makeDynamicArray([o]), unwrapMTreeOrderNodes(o.children))))
}

moveMTreeOrderNode(nodes : DynamicArray<MTreeOrderNode<?>>, id1 : ?, id2 : ?, dropOperation : DropOperation) -> void {
	maybeApply(
		findMTreeOrderNode(nodes, id1),
		\n1 -> {
			maybeApply(
				findMTreeOrderNode(nodes, id2),
				\n2 -> {
					moveMTreeOrderNode2(nodes, n1, n2, dropOperation);
				}
			);
		}
	);
}

moveMTreeOrderNode2(
	nodes : DynamicArray<MTreeOrderNode<?>>,
	node1 : MTreeOrderNode<?>,
	node2 : MTreeOrderNode<?>,
	dropOperation : DropOperation
) -> void {
	id = ref 0;

	iter(getDynamicArrayValue(nodes), \n -> {
		if (n.content == node1.content) {
			moveMTreeOrderNode2(n.children, node1, node2, dropOperation);

			if (dropOperation == DropAbove()) {
				insertDynamicArray(nodes, ^id, node2);
				id := ^id + 1;
			} else if (dropOperation == DropBelow()) {
				insertDynamicArray(nodes, ^id + 1, node2);
				id := ^id + 1;
			} else {
				insertDynamicArray(n.children, 0, node2);
			}

			id := ^id + 1;
		} else if (n.content == node2.content) {
			deleteDynamicArray(nodes, ^id);
		} else {
			moveMTreeOrderNode2(n.children, node1, node2, dropOperation);
			id := ^id + 1;
		}
	});
}

findMTreeOrderNode(nodes : DynamicArray<MTreeOrderNode<?>>, id : ?) -> Maybe<MTreeOrderNode<?>> {
	fold(getDynamicArrayValue(nodes), None(), \acc, n -> {
		if (isSome(acc)) {
			acc;
		} else if (n.content == id) {
			Some(n);
		} else {
			findMTreeOrderNode(n.children, id);
		}
	});
}

concatTrees(t1 : Tree<?, [??]>, t2 : Tree<?, [??]>) -> Tree<?, [??]> {
	foldTree(t2, t1, \k, v, t -> setTree(t, k, concat(lookupTreeDef(t, k, []), v)))
}

removeFirstFromTree(t : Tree<?, [??]>, k : ?) -> Tree<?, [??]> {
	eitherMap(
		lookupTree(t, k),
		\v ->
			if (length(v) > 1)
				setTree(t, k, tail(v))
			else
				removeFromTree(t, k),
		t
	)
}

fremoveFirstFromTree(t : Transform<Tree<?, [??]>>, k : ?) -> Transform<Tree<?, [??]>> {
	fselect(t, FLift(\tr ->
		eitherMap(
			lookupTree(tr, k),
			\v ->
				if (length(v) > 1)
					setTree(tr, k, tail(v))
				else
					removeFromTree(tr, k),
			tr
		)
	))
}

MTreeOrder2MTreeNodes(nodes : DynamicArray<MTreeNode<?>>, order : DynamicArray<MTreeOrderNode<?>>) -> DynamicArray<MTreeNode<?>> {
	filtersubmapDynamicArray(order, \n -> MTreeOrderNode2MTreeNode(nodes, n))
}

MTreeOrderNode2MTreeNode(nodes : DynamicArray<MTreeNode<?>>, node : MTreeOrderNode<?>) -> Transform<Maybe<MTreeNode<?>>> {
	treeNode = findMTreeNode(nodes, node);

	feitherMap(
		treeNode,
		\n -> {
			Some(
				MTreeNode(
					n.content,
					n.content2material,
					filtersubmapDynamicArray(node.children, \nn -> MTreeOrderNode2MTreeNode(nodes, nn)),
					n.style
				)
			)
		},
		const(None())
	);
}

findMTreeNode(nodes : DynamicArray<MTreeNode<?>>, node : MTreeOrderNode<?>) -> Transform<Maybe<MTreeNode<?>>> {
	findsubmapDynamicArray(nodes, \n -> if (n.content == node.content) const(Some(n)) else findMTreeNode(n.children, node));
}

applyMTreeLazyScrollMode(state : MTreeState<?>) -> (Tropic) -> Tropic {
	\node ->
		eitherMap(state.lazyScrollMode, \lsm -> {
			nodePosScale = make(zeroPositionScale);
			showItem = make(false);
			if (fgetValue(lsm.enabled))
				node
				|> (\t -> TShow(showItem, t))
				|> (\t -> TPositionScale(nodePosScale, t))
				|> (\t -> TConstruct([
						make3SubscribeTrigger(fnot(showItem), lsm.scrollWH, fstall(nodePosScale, 0), lsm.scrollPositionScale, \swh, ps, sps -> {
							y = ps.pos.y - sps.pos.y;
							if (y >= 0. && y < swh.height) nextDistinct(showItem, true);
						})
					], t))
			else
				node
			},
			node
		)
}

makeMTreeDragDropState(
	manager : MaterialManager,
	order : DynamicArray<MTreeOrderNode<?>>,
	onChange : Maybe<([MTreeOrderNode<?>]) -> void>
) -> MTreeDragDropState<?> {
	idContents = ref makeTree();
	contentIds = ref makeTree();

	content2id = \c -> {
		eitherFn(
			lookupTree(^idContents, c),
			\i -> i,
			\ -> {
				i = makeTropicTag(manager);

				idContents := setTree(^idContents, c, i);
				contentIds := setTree(^contentIds, i, c);

				i;
			}
		);
	};

	id2content = \i -> {
		lookupTree(^contentIds, i);
	};

	MTreeDragDropState(
		order,
		onChange,
		content2id,
		id2content,
		make(None())
	);
}

MTree2T(manager : MaterialManager, parent : MFocusGroup, m : MTree<?>, m2t : (Material, MFocusGroup) -> Tropic) -> Tropic {
	loadLazyButtonMode = tryExtractStruct(m.style, MTreeLazyLoad(0, None()));
	lazyLoadState = maybeMap(loadLazyButtonMode, makeMTreeLazyLoadState);

	lazyScrollMode =
		maybeMap(
			tryExtractStruct(m.style, MTreeLazyScrollMode(const(true))),
			\lsm -> MTreeLazyScrollModeState(
				lsm.enabled,
				makeWH(),
				make(zeroPositionScale)
			)
		);

	dragDropStateM =
		eitherFn(
			maybeMap(
				tryExtractStruct(m.style, MTreeOrder(makeDynamicArray([]))),
				\o ->
					makeMTreeDragDropState(manager, o.order, maybeMap(tryExtractStruct(m.style, MTreeOnOrderChange(nop1)), \oc -> oc.onChange))
			),
			\dragDropState ->
				Some(dragDropState),
			\ ->
				maybeMap(
					tryExtractStruct(m.style, MTreeOnOrderChange(nop1)),
					\oc ->
						makeMTreeDragDropState(manager, makeDynamicArray([]), Some(oc.onChange))
				)
		);

	children =
		eitherMap(lazyLoadState, \st -> subrangeDynamicArray(m.children, const(0), st.loadQuota), m.children)
		|> (\children -> eitherMap(dragDropStateM, \dragDropState -> MTreeOrder2MTreeNodes(children, dragDropState.order), children));

	singleSelection = tryExtractStruct(m.style, MSingleSelection(make(-1)));
	multiSelection = tryExtractStruct(m.style, MTreeMultiSelection(makeDynamicArray([])));
	itemsLength = tryExtractStruct(m.style, MItemsLength(make(0)));
	showSeparators = !contains(m.style, MTreeHideSeparators());

	state =
		MTreeState(
			dynamicSelectionIndexes(children),
			extractStruct(m.style, MTreeSelection(makeDynamicArray([]))).selected,
			tryExtractStruct(m.style, MTreeExpanded(makeDynamicArray([]))),
			extractStruct(m.style, getPrimaryColor(parent)),
			isSome(multiSelection),
			showSeparators,
			dragDropStateM,
			lazyScrollMode,
			loadLazyButtonMode
		);

	(\p -> (\p2 -> SelectMDynamicLines2T(
		manager,
		p2,
		children,
		\index, __, c, p3 ->
			MTreeNode2T(
				manager,
				p3,
				state,
				c,
				farray(index),
				m2t,
				const(showSeparators)
			)
			|> applyMTreeLazyScrollMode(state)
	)
	|> addUnfoldPanel(p2, m2t, m.children, lazyLoadState)
	|> TBorderBottom(1.)
	|> (\mtree : Tropic ->
		eitherMap(
			state.expanded,
			\stateExpanded ->
				TConstruct(
					[
						\ -> subscribeDynamicArray(state.selected, \selected ->
							iter(selected, \sel ->
								loop(
									subrange(sel, 0, length(sel) - 1),
									\s -> subrange(s, 0, length(s) - 1),
									\s -> length(s) > 0,
									\s -> pushDynamicArray(stateExpanded.expanded, s)
								)
							)
						)
					],
					mtree
				),
			mtree
		)
	)
	|> (\mtree : Tropic ->
		eitherMap(
			singleSelection,
			\ss ->
				TConstruct(
					[
						\ -> fBidirectionalLink(
							feither(
								elementAtDynamicArrayDef(
									submapDynamicArray(state.selected, \s -> findiDynamicArray(state.items, \i -> i == s)),
									const(0),
									None()
								),
								const(-1)
							),
							fsubselect(ss.selected, FLift(\s -> elementAtDynamicArrayDef(state.items, const(s), []))),
							\s -> nextDistinct(ss.selected, s),
							\s -> nextDynamicArray(state.selected, if (s == []) [] else [s])
						)
					],
					mtree
				),
			mtree
		)
	)
	|> (\mtree : Tropic ->
		eitherMap(
			multiSelection,
			\ms ->
				TConstruct(
					[
						\ -> bidirectionalLinkDynamicArray(
							filtersubmapDynamicArray(state.selected, \s -> findiDynamicArray(state.items, \i -> i == s)),
							filtersubmapDynamicArray(ms.selected, \s -> elementAtDynamicArray(state.items, const(s))),
							\s -> nextDynamicArray(ms.selected, s),
							\s -> nextDynamicArray(state.selected, s)
						)
					],
					mtree
				),
			mtree
		)
	)
	|> (\mtree ->
		TConstruct(
			eitherMap(
				state.dragDropState,
				\dragDropState -> {
					order = MTreeNodes2MTreeOrderNodes(m.children);
					orderA = unwrapMTreeOrderNodes(order);

					concat(
						[
							\ -> listen2DynamicArray(order, \__ -> {
								nextDynamicArray(dragDropState.order, getDynamicArrayValue(order));
							})
						],
						eitherFn(
							dragDropState.onChange,
							\oc ->
								[
									\ -> listen2DynamicArray(orderA, \__ -> {
										oc(getDynamicArrayValue(order));
									})
								],
							\ -> []
						)
					)
				},
				[]
			),
			mtree
		)
	)
	|> (\mtree ->
		eitherMap(
			itemsLength,
			\il ->
				TConstruct(
					[ \ -> fconnect(lengthMTreeNodes(children), il.length) ],
					mtree
				),
			mtree
		)
	))
	|> (\mtree : (MFocusGroup) -> Tropic ->
		eitherMap(
			state.lazyScrollMode,
			\lsm ->
				TPositionScale(lsm.scrollPositionScale,
					MScroll2T(manager, p, MScroll(MGetFocusGroup(mtree), TFillXY(), [MScrollWidthHeight(makeWH(), lsm.scrollWH)]), m2t)
				),
			mtree(p)
		)
	))
	|> (\mtree -> MComponentGroup2T(manager, parent, "MTree", extractMComponentGroupState(m.style), mtree))

}

hightlightFnDef(short : bool) -> (Transform<WidthHeight>) -> Material {
	\wh ->
		if (short)
			MPadA(16.0, 0.0,
				TRectangle([MFill(MGrey(200))], TSized(fwidthsubtract(wh, const(32.))))
			)
		else
			TRectangle([MFill(MGrey(200))], TSized(fheightaddition(wh, const(1.5))))
}

buildIconFnDef(isExpandedB : DynamicBehaviour<bool>, color : MColor) -> Material {
	MIconToggle(
		"remove_circle",
		[MIconSize(16.), FillOpacity(1.), color, MToggleFalseIcon("add_circle", [MIconSize(16.), FillOpacity(1.), color])],
		[MToggleValue(isExpandedB)]
	)
}

buildLineFnDef(contentBoxB : DynamicBehaviour<WidthHeight>, color : MColor) -> Material {
	TRectangle([MFill(color)], TFillWY(2.))
	|> (\t -> MBorderA(15., 0., 0., 6., t))
	|> MAlignStart
	|> (\m -> MTranslate(fselect(contentBoxB, FLift(\ch -> Point(0., 6.0 - 0.5 * ch.height))), m))
}

buildUnfoldPanelDef(onClick : () -> void) -> Material {
	MIcon("unfold_more", [])
	|> (\t -> MCenterIn(t, TFillXH(56.)))
	|> (\t -> MCopySize(t, \tr -> TRectangle([MFill(MGrey(200)), FillOpacity(0.5)], tr), false))
	|> (\t -> MComponent([MOnClick(onClick)], \__ -> t))
}

addUnfoldPanel(parent : MFocusGroup, m2t : (Material, MFocusGroup) -> Tropic, children : DynamicArray<MTreeNode>, state : Maybe<MTreeLazyLoadState>) -> (Tropic) -> Tropic {
	\t ->
		eitherMap(state, \st ->
			TLines2(t, TShow(flessi(st.loadQuota, lengthDynamicArray(children)), m2t(st.panel, parent))),
			t
		)
}

MTreeLazyLoadState(
	loadQuota : DynamicBehaviour<int>,
	panel : Material,
	resetFn : () -> void
);

makeMTreeLazyLoadState(loadLazy : MTreeLazyLoad) -> MTreeLazyLoadState {
	loadQuota = make(loadLazy.loadCount);
	resetFn = \ -> nextDistinct(loadQuota, loadLazy.loadCount);
	updateFn = \ -> nextDistinct(loadQuota, getValue(loadQuota) + loadLazy.loadCount);
	buildPanelFn = either(loadLazy.buildPanelFn, buildUnfoldPanelDef);
	MTreeLazyLoadState(loadQuota, buildPanelFn(updateFn), resetFn)
}

DropOperation ::= DropAbove, DropBelow, DropInside;
	DropAbove();
	DropBelow();
	DropInside();

MTreeNode2T(manager : MaterialManager, parent : MFocusGroup, state : MTreeState<?>, m : MTreeNode<?>, treeIndex : Transform<[int]>,
	m2t : (Material, MFocusGroup) -> Tropic, addVerticalSeparator : Transform<bool>) -> Tropic {

	lazyLoadState = maybeMap(state.loadLazyButtonMode, makeMTreeLazyLoadState);

	children = eitherMap(lazyLoadState, \st -> subrangeDynamicArray(m.children, const(0), st.loadQuota), m.children);
	childrenLength = lengthDynamicArray(children);
	expanded = extractStruct(m.style, MExpanded(make(true))).expanded;
	selected = extractStruct(m.style, MSelected(make(false))).selected;
	focused = extractStruct(m.style, MFocused(make(false))).focused;
	childSelected = tryExtractStruct(m.style, MChildSelected(make(false)));
	highlightOnSelect = extractStruct(m.style, MHighlightOnSelect(const(true))).enabled;
	shortHighlighter = contains(m.style, MShortHighlighter());

	highlightFn = extractStruct(m.style, MTreeNodeCustomHighlighter(hightlightFnDef(shortHighlighter))).fn;
	buildIconFn = extractStruct(m.style, MTreeNodeCustomIcon(buildIconFnDef)).fn;
	buildLineFn = extractStruct(m.style, MTreeNodeCustomLine(buildLineFnDef)).fn;

	focusState =
		replaceStructMany(
			m.style,
			[
				MOnClick(\ -> {
					nextDistinct(selected, if (state.multiSelection) !getValue(selected) else true);
					maybeApply(tryExtractStruct(m.style, MOnClick(nop)), \oc -> oc.click())
				}),
				MFocused(focused),
				MDefaultShortcutEnabled(const(false))
			]
		);

	contentBoxB = makeWH();

	// Drag and Drop
	hoveringIdB = make(-1);
	draggingB = make(false);
	hoveringCorner = make(None());
	dropOperation = fselect2(feither(hoveringCorner, const(TopLeft())), lengthDynamicArray(children), FLift2(\c, l -> {
		switch (c : Corner) {
			TopLeft(): {
				DropAbove();
			}
			TopRight(): {
				DropInside();
			}
			BottomLeft(): {
				if (l > 0)
					DropInside()
				else
					DropBelow();
			}
			BottomRight(): {
				DropInside();
			}
		}
	}));

	TLinesDir([
		(\p -> MComponent2T(
			manager,
			p,
			"MTreeNode",
			focusState,
			\focus ->
				TColsYCenterA([
					TVisible(
						fneq(childrenLength, 0),
						m2t(buildIconFn(expanded, extractStruct(m.style, state.color)), focus.parent)
					),
					TAttachBox(TGroup2(TFillX(), m2t(m.content2material(m.content), focus.parent)), contentBoxB)
				])
				|> (\t -> TAvailableHeight(t, const(0.)))
				|> (\t ->
					TDecorator(
						t,
						concat(
							eitherMap(
								tryExtractStruct(m.style, MTreeNodeCustomHighlighterOnHover(\__ -> TEmpty())),
								\highlightOnHover -> [Pair(focus.hover, \wh -> m2t(highlightOnHover.fn(wh), focus.parent))],
								[]
							),
							[Pair(fand(highlightOnSelect, selected), \wh -> m2t(highlightFn(wh), focus.parent))]
						),
						false
					)
				),
				m2t
		)
		|> (\f ->
			eitherMap(
				state.dragDropState,
				\dragDropState ->
					TLines2(
						TShow(
							fand(fneq(hoveringIdB, -1), feq(dropOperation, DropAbove())),
							TMutable(feitherMap(dragDropState.current, \fn -> fn(p), const(TEmpty())))
						),
						TCopySize(
							f,
							\sz -> {
								wh = makeWH();

								TShow(
									fneq(hoveringIdB, -1),
									TInteractive(
										[
											MouseMove2(\h, mi0 -> {
												mi = mi0();

												if (mi.inside) {
													if (mi.x < getValue(wh).width / 2.0) {
														if (mi.y < getValue(wh).height / 2.0) {
															nextDistinct(hoveringCorner, Some(TopLeft()));
														} else {
															nextDistinct(hoveringCorner, Some(BottomLeft()));
														}
													} else {
														if (mi.y < getValue(wh).height / 2.0) {
															nextDistinct(hoveringCorner, Some(TopRight()));
														} else {
															nextDistinct(hoveringCorner, Some(BottomRight()));
														}
													}
												}

												h;
											})
										],
										TRectangle(interactiveRectangleStyle, TInspect([ISize(wh)], sz))
									)
								)
							},
							true
						)
					),
				f
			)
		))
		|> (\f : (MFocusGroup) -> Tropic ->
			eitherFn(
				state.dragDropState,
				\dragDropState ->
					MDragDrop2T(
						manager,
						parent,
						MDragDrop(
							const(dragDropState.content2id(m.content)),
							MGetFocusGroup(f),
							[
								MOnDrop(\i ->
									maybeApply(
										dragDropState.id2content(i),
										\c ->
											moveMTreeOrderNode(dragDropState.order, m.content, c, fgetValue(dropOperation))
									)
								),
								MHoveringId(hoveringIdB),
								MDragging(draggingB),
								MOnDrag(\d -> {
									if (d) {
										next(
											dragDropState.current,
											Some(\pp ->
												TFrame(0.0, 4.0, [MFill(getPrimaryColor(manager)), FillOpacity(0.2)], TAlpha(const(0.5), f(pp)))
											)
										);
									}
								}),
								MDraggingContent(
									MShadow(
										const(8.0),
										MFrame(0.0, 4.0, [MFill(MBackgroundColor(getLightBackground(manager)))], MGetFocusGroup(f))
									)
								)
							]
						),
						m2t
					),
				\ -> f(parent)
			)
		),
		TShowLazy(
			fand(
				fOr(
					fneq(childrenLength, 0),
					if (isSome(state.dragDropState))
						fand(fneq(hoveringIdB, -1), feq(dropOperation, DropInside()))
					else
						const(false)
				),
				expanded
			),
			\ ->
				SelectMDynamicLines2T(
					manager,
					parent,
					children,
					\index, __, c, p -> {
						nextDistinct(expanded, true);

						MTreeNode2T(
							manager,
							p,
							state,
							c,
							farrayPush(treeIndex, index),
							m2t,
							if (state.showSeparators)
								flessi(index, fadditioni(childrenLength, const(-1)))
							else
								const(false)
						)
						|> applyMTreeLazyScrollMode(state)
					}
				)
				|> (\t ->
					TLines2(
						eitherMap(
							state.dragDropState,
							\dragDropState ->
								TShow(
									fand(fneq(hoveringIdB, -1), feq(dropOperation, DropInside())),
									TCols2(
										TFixed(24.0, 0.0),
										TMutable(feitherMap(dragDropState.current, \fn -> fn(parent), const(TEmpty())))
									)
								),
							TEmpty()
						),
						t
					)
				)
				|> (\t ->
					TCopySize(
						t,
						\tr ->
							TAvailable(m2t(buildLineFn(contentBoxB, extractStruct(m.style, state.color)), parent), tr),
						true
					)
				)
				|> (\t -> TAvailableHeight(t, const(0.)))
				|> addUnfoldPanel(parent, m2t, m.children, lazyLoadState)
		),

		MSeparator2T(manager, parent, true)
		|> TBorderLeftRight(16.)
		|> (\t -> TShow(addVerticalSeparator, t))
		|> (\t -> TAlignStartDir(t, parent.rtl))
	], parent.rtl)
	|> (\treeNode -> if (length(fgetValue(treeIndex)) > 1) TBorderDir(24., 0., 0., 0., treeNode, parent.rtl) else treeNode)
	|> (\treeNode ->
		eitherMap(
			state.dragDropState,
			\dragDropState ->
				TEasingMetrics(
					manager.manager.deltaTimer,
					\__ -> easingTransitionDuration,
					easingStandartCurve,
					draggingB,
					TLines2(
						TScale(fif(draggingB, const(Factor(1.0, 0.0)), const(Factor(1.0, 1.0))), treeNode),
						TShow(
							fand(fneq(hoveringIdB, -1), feq(dropOperation, DropBelow())),
							TCols2(
								TFixed(24.0, 0.0),
								TMutable(feitherMap(dragDropState.current, \fn -> fn(parent), const(TEmpty())))
							)
						)
					),
				),
			treeNode
		)
	)
	|> (\treeNode ->
		TConstruct(
			[
				makeSubscribeUns(selected, \s -> if (s) [interruptibleDeferUntilNextFrameRendered(\ -> nextDistinct(focused, true))] else []),
				\ ->
					fBidirectionalLink(
						fselectWithLast(fif(selected, treeIndex, const([])), FLift2(\v1, v2 -> Pair(v1, v2))),
						fsubselect(
							eitherMap(state.lazyScrollMode, \__ -> treeIndex, fstall(treeIndex, 0)),
							FLift(\ti -> containsDynamicArray(state.selected, ti))
						),
						\s -> {
							if (length(s.first) > 0 || length(s.second) > 0) {
								if (length(s.first) > 0 && (!contains(getDynamicArrayValue(state.items), s.first) || length(s.second) == 0)) {
									removeDynamicArray(state.selected, s.first) |> ignore;
								}

								if (length(s.second) > 0) {
									if (state.multiSelection) {
										pushDynamicArray(state.selected, s.second);
									} else {
										nextDynamicArray(state.selected, [s.second]);
									}
								}
							}
						},
						\s -> {
							nextDistinct(selected, s);
						}
					),
				\ ->
					eitherFn(
						state.expanded,
						\stateExpanded -> fBidirectionalLink(
							fselectWithLast(fif(expanded, treeIndex, const([])), FLift2(\v1, v2 -> Pair(v1, v2))),
							fsubselect(
								eitherMap(state.lazyScrollMode, \__ -> treeIndex, fstall(treeIndex, 0)),
								FLift(\ti -> containsDynamicArray(stateExpanded.expanded, ti))
							),
							\e -> {
								if (length(e.first) > 0 || length(e.second) > 0) {
									if (length(e.first) > 0) {
										removeDynamicArray(stateExpanded.expanded, e.first) |> ignore;
									}

									if (length(e.second) > 0) {
										pushDynamicArray(stateExpanded.expanded, e.second);
									}
								}
							},
							\e ->
								nextDistinct(expanded, e)
						),
						\ -> nop
					),
				\ -> {
					if (contains(getDynamicArrayValue(state.selected), fgetValue(treeIndex)))
						nextDistinct(selected, true);

					maybeApply(state.expanded, \stateExpanded ->
						if (contains(getDynamicArrayValue(stateExpanded.expanded), fgetValue(treeIndex)))
							nextDistinct(expanded, true)
					);

					\ -> if (!contains(getDynamicArrayValue(state.items), fgetValue(treeIndex))) {
						maybeApply(state.expanded, \stateExpanded -> removeDynamicArray(stateExpanded.expanded, fgetValue(treeIndex)) |> ignore);
						removeDynamicArray(state.selected, fgetValue(treeIndex)) |> ignore;
					}
				}
			]
			|> (\arr ->
				arrayPushMaybe(
					arr,
					maybeMap(
						childSelected,
						\cs ->
							\ -> fconnect(
								fsubselect(treeIndex, FLift(\i ->
									existsDynamicArray(state.selected, \si -> length(si) > length(i) && subrange(si, 0, length(i)) == i)
								)),
								cs.selected
							)
					)
				)
			)
			|> (\arr ->
				arrayPushMaybe(
					arr,
					maybeMap(
						lazyLoadState,
						\st ->
							makeSubscribe2(expanded, \e -> if (!e) st.resetFn())
					)
				)
			),
			treeNode
		)
	)
}