import runtime;
import ds/array;
import ds/set;
import math/math;

import formats/json/json;

// See https://github.com/thi-ng/umbrella/tree/develop/packages/transducers for inspiration
// which is © 2016 - 2020 Karsten Schmidt - Apache Software License 2.0 

/*
TODO:
https://docs.thi.ng/umbrella/transducers/modules/_index_.html
compose(a : Reducer<?, ??>, b : Reducer<??, ???>) -> Reducer<?, ???>
add, count, div, mul, sub, 
dup, duplicate, every, any, some, 

benchmark, cat/concat, choices/random, converge, fill, fillN, frequencies, 
groupBinary, groupbyMap, groupByObject, interleave, interpolate, keep,
permutations, last, line, mapNth, mapKeys, mapVals, matchFirst, matchLast,
mean, min, movingAverage, movingMedian, padLast, padSides, page, 
partition, partitionBy, range, repeat, repeatedly, reverse, run, 
sample, scan, step, 

drop, dropNth, dropWhile, take, takeLast, takeNth, takeWhile, trim

throttle,
toggle, debug/trace, tween, wordWrap, 

parallel/multiplex, 

Wishlist to match flow interface:
	map, fold, filter, filtermap: // Available
	iter, 
	hash, 
	find, 
	forall, exists, 
	serialization, deserialization, 
	conversions, math operations, reflection of types, reflection of code…
	trim, left, right, ... other common string-based methods

*/

export {
	// A reducer represents a processing pipeline.
	// The individual values we send in for processing is ?,
	// while the resulting type of the pipeline is ?? (i.e. acc)
	Reducer(
		// The initial value
		init : () -> ??, 
		// Each step is done here
		reduce : (acc : ??, value : ?) -> ReducerResult<??>,
		// The final value is transformed here
		final : (acc : ??) -> ??, 
	);

		// At each processing step, we either get a value or stop processing
		ReducerResult<?> ::= ReduceAcc<?>, Reduced<?>;
			// When the reduction should continue
			ReduceAcc(value : ?);
			// Used when we are fully reduced and should stop
			Reduced(value : ?);

	//
	// Drivers (all linear time)
	//

	// Evaluate a reducer on this array of values
	rreduce(r : Reducer<?, ??>, v : [?]) -> ??;

	// Evaluate a reducer on this list of values
	rreduceList(r : Reducer<?, ??>, v : List<?>) -> ??;

	// Evaluate a reducer on this tree of values
	rreduceTree(r : Reducer<Pair<?, ??>, ???>, v : Tree<?, ??>) -> ???;

	// Evaluate a reducer on the keys from this tree
	rreduceTreeKeys(r : Reducer<?, ???>, v : Tree<?, ??>) -> ???;

	// Evaluate a reducer on this set of values
	rreduceSet(r : Reducer<?, ??>, set : Set<?>) -> ??;

	// Char-wise iteration
	rreduceString(r : Reducer<string, ?>, str : string) -> ?;
	// Char-wise iteration as unicode
	rreduceChars(r : Reducer<int, ?>, str : string) -> ?;

	rreduceJson(reducer : Reducer<JsonAssign, ??>, json : Json) -> ??;
		// An assignment is a path to the place in the structure where it belongs and the value to put there
		JsonAssign(lvalue : JsonLValue, value : Json);

		// An "address" in an JSON object to place a value
		JsonLValue ::= JsonLeaf, JsonLObject, JsonLArray;
			// Immediate value
			JsonLeaf();
			// Place the value inside this key in an object
			JsonLObject(key : string, rvalue : JsonLValue);
			// Add the value to this place inside this array
			JsonLArray(rvalue : JsonLValue);

	// TODO: multimap, tuples, graphs, dlist, behaviour/transforms

	//
	// Reducers
	//

	// Collect items in a list - O(n)
	rlist() -> Reducer<?, List<?>>;
	// Collect items in array - O(n^2)
	rarray() -> Reducer<?, [?]>;
	// Collect items in a set - O(n log n)
	rset() -> Reducer<?, Set<?>>;
	// Collect items in a tree - O(n log n). Pairs are key=value
	rtree() -> Reducer<Pair<?, ??>, Tree<?, ??>>;
	// Prints each item
	rdebug() -> Reducer<?, bool>;

	// Collect chars into a string - O(n^2)
	rstring() -> Reducer<string, string>;
	// Collect char codes into a string - O(n^2)
	rchars() -> Reducer<int, string>;
	// You can use rlist() and the list2string to get O(n)

	// Construct a JSON object based on a bunch of assignments
	// This is O(n^2) time, unfortunately, so beware!
	rjson() -> Reducer<JsonAssign, Json>;

	// Fold the values using this initial value, and this function
	rfold(init : ??, fn : (??, ?) -> ??) -> Reducer<?, ??>;

	// graphs, dlist, behaviour/transforms

	//
	// Transducers
	//

	// Transforms a reducer by a fn
	rmap(r : Reducer<?, ???>, fn : (??) -> ?) -> Reducer<??, ???>;

	// Keeps only distinct values - n log n
	rdistinct(r : Reducer<?, ??>) -> Reducer<?, ??>;

	// Keeps values that match the condition
	rfilter(r : Reducer<?, ??>, fn : (?) -> bool) -> Reducer<?, ??>;

	// Keeps the maximum result value - this is different from the package, where it is the input values
	rmaxOutput(r : Reducer<?, ??>) -> Reducer<?, ??>;

	// A similar thing for input is maybe also relevant
	// rmaxInput(r : Reducer<?, ?>) -> Reducer<?, ?>;
}

//
// Drivers
//

rreduce(r : Reducer<?, ??>, vs : [?]) -> ?? {
	acc = doRReduce(r, r.init(), vs, 0);
	r.final(acc);
}

doRReduce(r : Reducer<?, ??>, acc : ??, vs : [?], i : int) -> ?? {
	if (i < length(vs)) {
		v = r.reduce(acc, vs[i]);
		switch (v) {
			ReduceAcc(nacc): {
				doRReduce(r, nacc, vs, i + 1)
			}
			Reduced(val): val;
		}
	} else acc;
}

rreduceList(r : Reducer<?, ??>, vs : List<?>) -> ?? {
	acc = doRReduceList(r, r.init(), vs);
	r.final(acc);
}

doRReduceList(r : Reducer<?, ??>, acc : ??, vs : List<?>) -> ?? {
	switch (vs) {
		EmptyList(): acc;
		Cons(v1, t): {
			v = r.reduce(acc, v1);
			switch (v) {
				ReduceAcc(nacc): {
					doRReduceList(r, nacc, t)
				}
				Reduced(val): val;
			}
		}
	}
}

rreduceTree(r : Reducer<Pair<?, ??>, ???>, tree : Tree<?, ??>) -> ??? {
	acc = doRReduceTree(r, r.init(), tree);
	r.final(acc.value);
}

doRReduceTree(r : Reducer<Pair<?, ??>, ???>, acc : ???, tree : Tree<?, ??>) -> ReducerResult<???> {
	switch (tree : Tree) {
		TreeEmpty(): ReduceAcc(acc);
		TreeNode(k, v, left, right, depth): {
			acc1 = doRReduceTree(r, acc, left);
			switch (acc1) {
				ReduceAcc(nacc1): {
					// OK, there is more
					acc2 = r.reduce(nacc1, Pair(k, v));
					switch (acc2) {
						ReduceAcc(nacc2): {
							// OK, there is more
							doRReduceTree(r, nacc2, right);
						}
						Reduced(value): acc2; // We are done
					}
				}
				Reduced(value): acc1;	// We are done
			}
		}
	}
}

rreduceTreeKeys(r : Reducer<?, ???>, tree : Tree<?, ??>) -> ??? {
	acc = doRReduceTreeKeys(r, r.init(), tree);
	r.final(acc.value);
}

doRReduceTreeKeys(r : Reducer<?, ???>, acc : ???, tree : Tree<?, ??>) -> ReducerResult<???> {
	switch (tree : Tree) {
		TreeEmpty(): ReduceAcc(acc);
		TreeNode(k, v, left, right, depth): {
			acc1 = doRReduceTreeKeys(r, acc, left);
			switch (acc1) {
				ReduceAcc(nacc1): {
					// OK, there is more
					acc2 = r.reduce(nacc1, k);
					switch (acc2) {
						ReduceAcc(nacc2): {
							// OK, there is more
							doRReduceTreeKeys(r, nacc2, right);
						}
						Reduced(value): acc2; // We are done
					}
				}
				Reduced(value): acc1;	// We are done
			}
		}
	}
}

rreduceSet(r : Reducer<?, ??>, set : Set<?>) -> ?? {
	rreduceTreeKeys(r, set.tree);
}

rreduceString(r : Reducer<string, ?>, str : string) -> ? {
	acc = doRReduceString(r, r.init(), 0, str);
	r.final(acc);
}

doRReduceString(r : Reducer<string, ?>, acc : ?, i : int, str : string) -> ? {
	if (i < strlen(str)) {
		val = r.reduce(acc, getCharAt(str, i));
		switch (val) {
			ReduceAcc(nacc): {
				doRReduceString(r, nacc, i + 1, str)
			}
			Reduced(nacc): nacc;
		}
	} else acc;
}

rreduceChars(r : Reducer<int, ?>, str : string) -> ? {
	acc = doRReduceChars(r, r.init(), 0, str);
	r.final(acc);
}

doRReduceChars(r : Reducer<int, ?>, acc : ?, i : int, str : string) -> ? {
	if (i < strlen(str)) {
		val = r.reduce(acc, getCharCodeAt(str, i));
		switch (val) {
			ReduceAcc(nacc): {
				doRReduceChars(r, nacc, i + 1, str)
			}
			Reduced(nacc): nacc;
		}
	} else acc;
}


//
// Reducers
//

rlist() -> Reducer<?, List<?>> {
	Reducer(\ -> makeList(), \acc : List<?>, v : ? -> ReduceAcc(Cons(v, acc)), \acc : List<?> -> acc);
}

rarray() -> Reducer<?, [?]> {
	Reducer(\ -> [], \acc : [?], v : ? -> ReduceAcc(arrayPush(acc, v)), \acc : [?] -> acc);
}

rset() -> Reducer<?, Set<?>> {
	Reducer(\ -> makeSet(), \acc : Set<?>, v : ? -> ReduceAcc(insertSet(acc, v)), \acc : Set<?> -> acc);
}

rtree() -> Reducer<Pair<?, ??>, Tree<?, ??>> {
	Reducer(\ -> makeTree(), \acc : Tree<?, ??>, v : Pair<?, ??> -> ReduceAcc(setTree(acc, v.first, v.second)), \acc : Tree<?, ??> -> acc);
}

rstring() -> Reducer<string, string> {
	Reducer(\ -> "", \acc : string, v : string -> ReduceAcc(acc +v), \acc -> acc);
}

rchars() -> Reducer<int, string> {
	Reducer(\ -> "", \acc : string, v : int -> ReduceAcc(acc + fromCharCode(v)), \acc -> acc);
}


rfold(init : ??, fn : (??, ?) -> ??) -> Reducer<?, ??> {
	Reducer(\ -> init, \acc, v -> ReduceAcc(fn(acc, v)), \acc : ?? -> acc);
}


rdebug() -> Reducer<?, bool> {
	Reducer(\ -> false,
		\acc, v : ? -> {
			println(v);
			ReduceAcc(acc);
		}, 
		\acc -> acc
	)
}


rreduceJson(reducer : Reducer<JsonAssign, ??>, json : Json) -> ?? {
	acc = doRReduceJson(reducer, reducer.init(), \v -> v, json);
	reducer.final(acc.value);
}

doRReduceJson(reducer : Reducer<JsonAssign, ??>, acc : ??, lvalueWrap : (JsonLValue) -> JsonLValue, json : Json) -> ReducerResult<??> {
	switch (json) {
		JsonObject(members): {
			doRReduceJsonObject(reducer, acc, lvalueWrap, 0, members);
		}
		JsonArray(elements): {
			doRReduceJsonArray(reducer, acc, lvalueWrap, 0, elements);
		}
		JsonNull(): reducer.reduce(acc, JsonAssign(lvalueWrap(JsonLeaf()), json));
		JsonBool(v): reducer.reduce(acc, JsonAssign(lvalueWrap(JsonLeaf()), json));
		JsonDouble(v): reducer.reduce(acc, JsonAssign(lvalueWrap(JsonLeaf()), json));
		JsonString(v): reducer.reduce(acc, JsonAssign(lvalueWrap(JsonLeaf()), json));
	}
}

doRReduceJsonObject(reducer : Reducer<JsonAssign, ??>, acc : ??, lvalueWrap : (JsonLValue) -> JsonLValue, i : int, members : [Pair<string, Json>]) -> ReducerResult<??> {
	if (i < length(members)) {
		member = members[i];
		lvalue = \v -> lvalueWrap(JsonLObject(member.first, v));
		v = doRReduceJson(reducer, acc, lvalue, member.second);
		switch (v) {
			ReduceAcc(nacc): {
				doRReduceJsonObject(reducer, nacc, lvalueWrap, i + 1, members)
			}
			Reduced(val): v;
		}
	} else ReduceAcc(acc);
}

doRReduceJsonArray(reducer : Reducer<JsonAssign, ??>, acc : ??, lvalueWrap : (JsonLValue) -> JsonLValue, i : int, elements : [Json]) -> ReducerResult<??> {
	if (i < length(elements)) {
		lvalue = \v -> lvalueWrap(JsonLArray(v));
		element = elements[i];
		v = doRReduceJson(reducer, acc, lvalue, element);
		switch (v) {
			ReduceAcc(nacc): {
				doRReduceJsonArray(reducer, nacc, lvalueWrap, i + 1, elements)
			}
			Reduced(val): v;
		}

	} else ReduceAcc(acc);
}

rjson() -> Reducer<JsonAssign, Json> {
	Reducer(
		\ -> JsonNull(),
		\acc, val -> {
			ReduceAcc(rjsonAssign(acc, val))
		},
		\acc -> {
			acc
		}
	)
}

rjsonAssign(acc : Json, assign : JsonAssign) -> Json {
	switch (assign.lvalue) {
		JsonLeaf(): assign.value;
		JsonLObject(key, lvalue2): {
			fieldValue = getJsonFieldValue(acc, key, JsonNull());
			setJsonField(acc, key, rjsonAssign(fieldValue, JsonAssign(lvalue2, assign.value)));
		}
		JsonLArray(lvalue2): {
			pushJsonValue(acc, rjsonAssign(JsonNull(), JsonAssign(lvalue2, assign.value)));
		}
	}
}

//
// Transducers
//

rmap(r : Reducer<?, ???>, fn : (??) -> ?) -> Reducer<??, ???> {
	Reducer(
		r.init,
		\acc : ???, v : ?? -> {
            r.reduce(acc, fn(v))
		},
		r.final,
	)
}

rdistinct(r : Reducer<?, ??>) -> Reducer<?, ??> {
	seen = ref makeSet();
	Reducer(
		r.init,
		\acc, v -> {
			if (containsSet(^seen, v)) ReduceAcc(acc)
			else {
				seen := insertSet(^seen, v);
				r.reduce(acc, v)
			}
		},
		r.final,
	)
}

rfilter(r : Reducer<?, ??>, fn : (?) -> bool) -> Reducer<?, ??> {
	Reducer(
		r.init,
		\acc, v -> {
			if (fn(v)) {
				r.reduce(acc, v)
			} else ReduceAcc(acc)
		},
		r.final,
	)
}


rmaxOutput(r : Reducer<?, ??>) -> Reducer<?, ??> {
	Reducer(
		r.init,
		\acc, v -> {
			val = r.reduce(acc, v);
			switch (val) {
				Reduced(nacc): Reduced(max(acc, nacc));
				ReduceAcc(nacc): ReduceAcc(max(acc, nacc));
			}
		},
		r.final,
	)
}

//
// Testing
//

/*
main() {
	// To combine into an array
	reducer = rmap(rarray(), i2s);
	f = rreduce(rfilter(rdistinct(reducer), \n -> n % 3 == 0), [1, 1, 2, 3, 2, 5, 2, 1]);
	println(f);	// ["3"]

	f2 = rreduceTree(rarray(), setTree(makeTree1(2, -2), 1, -1));
	println(f2);	// [Pair(1, -1), Pair(2, -2)]

	f3 = rreduceSet(rarray(), buildSet([3,1,2,3,6]));
	println(f3);	// [1,2,3,6]

	json1 = parseJson("
		{
			a: 0,
			b: [
				1,
				2,
				3
			],
			c: {
				d: true,
				e: null
			}
		}
	");

	// Reduce to a list of json-assignments
	jassigns : [JsonAssign] = rreduceJson(rarray(), json1);
	iter(jassigns, println);
	// JsonAssign(JsonLObject("a", JsonLeaf()), JsonDouble(0.0))
	// JsonAssign(JsonLObject("b", JsonLArray(JsonLeaf())), JsonDouble(1.0))
	// JsonAssign(JsonLObject("b", JsonLArray(JsonLeaf())), JsonDouble(2.0))
	// JsonAssign(JsonLObject("b", JsonLArray(JsonLeaf())), JsonDouble(3.0))
	// JsonAssign(JsonLObject("c", JsonLObject("d", JsonLeaf())), JsonBool(true))
	// JsonAssign(JsonLObject("c", JsonLObject("e", JsonLeaf())), JsonNull())

	// Then restruct as JSON object again
	json2 = rreduce(rjson(), jassigns);
	println(json2stringFormatted(json2));
	// Same output as the input

	leafs = rreduce(rmap(rarray(), \assign : JsonAssign -> assign.value), jassigns);
	println(leafs);
	// [JsonDouble(0.0), JsonDouble(1.0), JsonDouble(2.0), JsonDouble(3.0), JsonBool(true), JsonNull()]

	s1 : [string] = rreduceString(rarray(), "Char by Char");
	s2 : [int] = rreduceChars(rarray(), "Char by Char");

	quit(0);
}
*/
