import ds/tree;
import fusion;

export {
	// Given a dynamic array of values, construct a factor that can produce a tree view of it
	getCachedTree(data : Transform<[?]>, getKey : (?) -> ??) -> CachedTree<?, ??>;
	// When you need a tree of the data, call getTree. It will be cached when possible
	CachedTree(getTree : () -> Tree<??, ?>, dispose : () -> void);
}

getCachedTree(data : Transform<[?]>, getKey : (?) -> ??) -> CachedTree<?, ??> {
	hasData = ref false;
	cachedTree = ref makeTree();
	us = fsubscribe(data, \__ -> {
		hasData := false;
		cachedTree := makeTree();
	});
	CachedTree(
		\ -> {
			if (!^hasData) {
				cdata = fgetValue(data);
				cachedTree := fold(cdata, makeTree(), \acc, d -> {
					setTree(acc, getKey(d), d);
				});
				hasData := true;
			}
			^cachedTree
		},
		\ -> callList(us)
	)
}
